plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.android.gms.oss-licenses-plugin'
}

apply from: rootProject.file("android_common.gradle")

android {
    defaultConfig {
        applicationId "com.example.hackernewschecker"
        versionName "1.5"
        versionCode generateVersionCode(versionName)
    }

    buildTypes {
        // debug 版の設定を引き継ぎつつ、Proguard を有効にする
        check {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".check"
        }

        release {
            // 公開するアプリなので、Release 用の設定（keystore）は行わない
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // ソースディレクトリを java から kotlin に変更する
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    // ViewBinding を有効化
    android.buildFeatures.viewBinding = true
}

dependencies {
    // Module
    implementation project(":dto")
    implementation project(":view_common")
    implementation project(":how_to")
    implementation project(":usecase")
    implementation project(":repository")

    // base
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.appcompat:appcompat:$app_compat_version"

    // Layout
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_layout_version"

    // dagger2
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // okHttp3
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_ver"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_ver"

    // Moshi
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // OSS License
    implementation "com.google.android.gms:play-services-oss-licenses:$oss_licenses_version"
}

// versionName から versionCode を作成する
static def generateVersionCode(String versionName) {
    def versionArray = versionName.split("\\.")
    return versionArray[0].toInteger() * 100 + versionArray[1].toInteger()
}